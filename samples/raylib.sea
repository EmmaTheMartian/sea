use std

raw[
#include <raylib.h>
#include <time.h>
]

tem (T: type, width: int, height: int) {
	rec Grid(width: int, height: int, elements: T[width][height])

	fun grid_set(grid: ^Grid{T, width, height}, x: int, y: int, it: T) -> grid^.elements[x][y] = it
	fun grid_get(grid: ^Grid{T, width, height}, x: int, y: int): T -> ret grid^.elements[x][y]
}

gen Grid{Color, 10, 10}
gen grid_set{Color, 10, 10}
gen grid_get{Color, 10, 10}

var tile_size: int = 20
var world: Grid{Color, 10, 10} = new Grid{Color, 10, 10}(0)

raw[inline]
fun render() {
	for y in 0 to 10 {
		for x in 0 to 10 {
			DrawRectangle(y * tile_size, x * tile_size, tile_size, tile_size, grid_get{Color, 10, 10}(ref world, x, y))
		}
	}

	DrawFPS(10, 10)
}

raw[inline]
fun rand_u8(): u8 -> ret (rand() / (RAND_MAX as float) * 255) as u8

raw[inline]
fun random_color(): Color -> ret new Color(rand_u8(), rand_u8(), rand_u8(), rand_u8())

fun main(): int {
	srand(time(NULL))

	SetTargetFPS(60)
	InitWindow(800, 600, c"Raylib with Sea")

	for y in 0 to 10 {
		for x in 0 to 10 {
			grid_set{Color, 10, 10}(ref world, x, y, random_color())
		}
	}

	for not WindowShouldClose() {
		BeginDrawing()
		ClearBackground(RAYWHITE)
		render()
		EndDrawing()
	}

	CloseWindow()

	ret 0
}
